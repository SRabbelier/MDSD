module to-yuml

imports include/Entity
imports include/Diagram

rules

// mappers

  declare-all = alltd(declare)

  declare : e@Entity(name, _) -> e
  with rules (          
      EntityDeclaration : name -> e
  )

  declare : e@InheretingEntity(name, inherits, _) -> e
  with rules (
      EntityDeclaration : name -> e
      InheritsFrom : name -> inherits
  )

// projectors

rules

  define : Class(name, Properties([])) ->
    DefinitionLine(Definition(EmptyClass(name), NoAttrs()))

  define : c@Class(name, _) ->
    DefinitionLine(Definition(c, NoAttrs()))

  relate(|lhs, rhs, lhs-attrs, rhs-attrs) : _ -> 
    RelationLine(Relation(LHS(lhs-definition, lhs-attrs), Line(), RHS(rhs-attrs, rhs-definition)))
    where lhs-definition := Definition(EmptyClass(lhs), NoAttrs())
    where rhs-definition := Definition(EmptyClass(rhs), NoAttrs())

  get-name : Entity(name, _) -> name

  declare-inherits : InheretingEntity(name, _, _) ->
    InheretanceLine(Definition(simple, NoAttrs()),
                    Definition(resolved, NoAttrs()))
    where from := <InheritsFrom>name
    where simple := EmptyClass(name)
    where from-name := <EntityDeclaration; get-name>from
    where resolved := EmptyClass(from-name)

  declare-referenced(|cname, lhs) : SetType(name) ->
    <relate(|cname, name, lhs-attrs, rhs-attrs)>
    where lhs-attrs := [Note(lhs)]
    where rhs-attrs := [RightDirectional(), AnyCardinality()]

  declare-referenced(|cname, lhs) : Type(name) ->
    <relate(|cname, name, lhs-attrs, rhs-attrs)>
    where lhs-attrs := [Note(lhs)]
    where rhs-attrs := [RightDirectional()]

  declare-composite(|cname, lhs) : Type(name) ->
    <relate(|cname, name, lhs-attrs, rhs-attrs)>
    where lhs-attrs := [Note(lhs), Composition()]
    where rhs-attrs := [RightDirectional()]

  declare-property(|cname) : CompositeProperty(name, referenced) ->
    [<declare-composite(|cname, name)>referenced]

  declare-attributes : InverseAttribute(Field(lhs, rhs)) -> <fail>

  declare-property(|cname) : ReferenceProperty(name, referenced, NoAttributes()) ->
    [<declare-referenced(|cname, name)>referenced]

  declare-property(|cname) : ReferenceProperty(name, referenced, Attributes(attributes)) ->
    [<declare-referenced(|cname, name)>referenced | <filter(declare-attributes)>attributes]
    
  declare-properties : Entity(name, properties) ->
    <filter(declare-property(|name)); concat>properties

rules

  to-yuml : Module(name, def) -> 
    Diagram(<map(to-yuml); concat>def)

  to-yuml : Entity(name, []) ->
     [<define>EmptyClass(name)]

  to-yuml : e@Entity(name, properties) ->
    [<define>Class(name, props) | <declare-properties>e]
    where props := Properties(<filter(to-yuml)>properties)

  to-yuml : e@InheretingEntity(name, inherits, properties) ->
    [<declare-inherits>e | <to-yuml>Entity(name, properties)]

  to-yuml : Property(name, type) ->
    Property(name)
