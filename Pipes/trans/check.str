module check

imports
  libstratego-lib
  include/Pipes
  lib/editor-common.generated
  analyze
  rename
	
rules
  eq-len:
  	[[a|a'], [b|b']] -> res
  	where res := <eq-len>[a', b']
  eq-len:
  	[[], [_]] -> <fail>
  eq-len:
  	[[_], []] -> <fail>
  eq-len:
  	[[], []] -> <id>
        
  get-args:
  	LambdaFunction(_, None()) -> []  
  	
  get-args:
  	LambdaFunction(_, FunctionArguments(t)) -> t

  get-args:
  	Pipe(_, _, NoArguments(), _, _) -> []

  get-args:
  	Pipe(_, _, Arguments(ArgumentList(a)), _, _) -> a
  
  get-args:
  	Native(_, _, NoArguments(), _) -> []
	  
  get-args:
  	Native(_, _, Arguments(ArgumentList(a)), _) -> a

  get-inputs:
  	Pipe(_, NoInput(), _, _, _) -> []
  get-inputs:
  	Pipe(_, ImplicitInput(), _, _, _) -> [Stream("stream")]
  get-inputs:
  	Pipe(_, ExplicitInput(StreamList(list)), _, _, _) -> list

  get-outputs:
	Pipe(_, _, _, _, ImplicitOutput()) -> [Stream("stream")]

  get-outputs:
	Pipe(_, _, _, _, ExplicitOutput(StreamList(list))) -> list
	
  checkParam(|target):
  	[[DefaultedArgument(_, param)|rest], args] -> errs
  	with errs :=  <checkParam(|target)>[rest, args]

  checkParam(|target):
  	[[PipeArgument(PipeName(param))|rest], args] -> errors
  	with errs :=  <checkParam(|target)>[rest, args]
  	with (match := <fetch-elem(?FunctionArgument(_, param))> args) < errors:=<pipeParam(|errs)>match + errors:=<missingParam(|errs, target)>param

  checkParam(|target):
  	[[Argument(Variable(param))|rest], args] -> errors
  	with errs :=  <checkParam(|target)>[rest, args]
  	with (<fetch-elem(?FunctionArgument(_, param))> args) < errors:=errs + errors:=<missingParam(|errs, target)>param
  
  checkParam(|target):
  	[[], _] -> []	

  pipeParam(|error):
  	FunctionArgument(LambdaRef(_), _) -> error
  
  pipeParam(|error):
  	FunctionArgument(a, _) -> [(a, $[Expecting a Pipe argument])]

  missingParam(|errors, target):
  	x -> [(target, $[Missing argument '[x]']) | errors]
  
  hasParam(|x):
  	param -> par
  	where par := <fetch-elem(?Argument(Variable(x))) <+ fetch-elem(?PipeArgument(PipeName(x))) <+ fetch-elem(?DefaultedArgument(_, Variable(x)))>param 
  
  checkArgs:
  	[param, [f@FunctionArgument(arg,x)|rest]] -> errors
  	with errs := <checkArgs>[param, rest]
  	with (match := <hasParam(|x)>param) <  errors:=<pipeArg(|errs)>[arg, match] + errors:=<invArg(|errs)>f  
  	
  checkArgs:
  	[_, []] -> []
  
  pipeArg(|errs):
  	[LambdaRef(_), PipeArgument(_)]-> errs

  pipeArg(|errs):
  	[a@LambdaRef(_), _] -> [(a, $[Argument is not a Pipe argument])]
  
  pipeArg(|errs):
  	[_, _] -> errs

  invArg(|errors):
  	f@FunctionArgument(_,x) -> [(x, $[Invalid argument '[x]'])| errors]

  is-empty:
  	[] -> <id>
  is-empty:
  	[_] -> <fail>

  	
  stream-initialised(|target):
  	s@Stream(name) -> (target, $[Stream $[name] not initialised])
  	where not(<InitStream>name)
  	
  // Reports an error if a property type is undefined.
  // This error is reported on the type name 'type'.
  constraint-error:
    LambdaFunction(p@PipeName(name), _) -> (p, $[Pipe '[name]' is not defined])
    where
      not(<GetPipe> name) // no entity for this type
    where
      not(<InitPipe> name) // not a pipe argument
  
  constraint-error:
  	Run(p@PipeName(name)) -> (p, $[Pipe '[name]' is not defined])
    where
      not(<GetPipe> name) // no entity for this type
  
  constraint-error:
  	LambdaFunction(PipeName(name), Some(FunctionArguments(args))) -> <concat>[errors, errors2]
    where pargs := <get-args><GetPipe> name 
	  // Check that all arguments are expected parameters
    with errors := <checkArgs>[pargs, args]
    with errors2 := <checkParam(|name)>[pargs, args]
    
  constraint-error:
	  // Check that all required arguments are provided
  	f@LambdaFunction(PipeName(name), None()) -> errors
    where pargs := <get-args><GetPipe>name
    with errors := <checkParam(|name)>[pargs, []]
    
  constraint-error:
  	FunctionApplication(_, LambdaFunction(p@PipeName(name),_), ApplyFunction(ApplicationTarget(StreamList(input)))) -> (p, $[Pipe '[name]' expects [expected_size] streams but got [actual_size]])
  	with actual_size := <length>input
  	where pipe := <GetPipe>name
  	with expected_input := <get-inputs>pipe
  	with expected_size := <length>expected_input
  	// where not(<eq>[expected_size, actual_size]) // LOLWTF?!!OMGBBQ
  	where not(<eq-len>[input, expected_input]) 

  constraint-error:
  	FunctionApplication(MultipleStreamAssignment(StreamList(streams)), LambdaFunction(p@PipeName(name),_),_) -> (p, $[Pipe '[name]' returns [expected_size] streams but has [actual_size] assigned])
  	with actual_size := <length>streams
  	where pipe:= <GetPipe>name
  	with expected_output := <get-outputs>pipe
  	with expected_size := <length>expected_output
  	where not(<eq-len>[streams, expected_output])
  
  constraint-error:
  	LambdaRef(LambdaFunction(p@PipeName(name),_)) -> (p, $[Pipe arguments receive one stream, '[name]' expects [expected_size]])
  	where pipe:= <GetPipe>name
  	with expected_input := <get-inputs>pipe
  	with expected_size := <length>expected_input
	where not(<eq-len>[["one"], expected_input])
  
  constraint-error:
  	LambdaRef(LambdaFunction(p@PipeName(name),_)) -> (p, $[Pipe arguments must return one stream, '[name]' returns [expected_size]])
  	where pipe:= <GetPipe>name
  	with expected_output := <get-outputs>pipe
  	with expected_size := <length>expected_output
	where not(<eq-len>[["one"], expected_output])
	  	
  constraint-error:
  	Pipe(_, _, _, _, out@ExplicitOutput(StreamList(streams))) -> errors
  	with errors1 := <collect-all(constraint-error2, conc)>
  	with errors2 := <collect-all(stream-initialised(|out), conc)>streams
	with errors := <concat>[errors1, errors2] 

  constraint-error:
  	x@IsEmptyCondition(streams) -> error
  	with error := <collect-all(stream-initialised(|x), conc)>streams
	
  constraint-error:
  	app@ApplyFunction(ApplicationTarget(StreamList(streams))) -> errors
  	with errors := <collect-all(stream-initialised(|app), conc)>streams

  constraint-error:
  	var@Variable(name) -> (var, $[Variable [name] is not initialized])
  	where not(<InitVariable>name)
  
  // Reports an error if an entity is defined more than once.
  // This constraint is implemented by testing if the "bag" of
  // all entities x is not a singleton list.
  constraint-error2:
    Pipe(PipeName(x), _, _, _, _) -> (x, $[Duplicate pipe name '[x]'])
    where
      not(<bagof-GetCustom> x => [_])  

  constraint-error:
    Native(PipeName(x), _, _, _) -> (x, $[Duplicate native name '[x]'])
    where
      not(<bagof-GetNative> x => [_])
  
  constraint-error:
  	Load(x) -> (x, $[Could not load module '[x]'])
  	where
  	  <FailedImports>x
  
  constraint-warning:
    Pipe(PipeName(x), _, _, _, _) -> (x, $[Shadowing native pipe])
    where
      (<GetNative> x)

  constraint-note:
    Pipe(x@PipeName("Tim"), _, _, _, _) -> (x, $[I'm the incredible machine.])

  constraint-note:
    Pipe(x@PipeName("Sverre"), _, _, _, _) -> (x, $[I'm are a CLI addict])
    
  ast-constraint-warning:
  	x@Stream("implicit_stream") -> (x, $[Use of 'implicit_stream' is discouraged; it's ugly and horrid.])
  
  ast-constraint-warning:
  	 x@MultipleStreamAssignment(StreamList([Stream(name)])) -> (x, $[Correct English would be to say '$[name] becomes'.])
  	
