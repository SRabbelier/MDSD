module python

imports
    libstratego-lib
    libstratego-gpp
    libstratego-aterm
    include/Pipes
    lib/editor-common.generated
    lib/StrategoPython
    analyze

rules
  extract-variables : Argument(v) -> v
  extract-variables : DefaultedArgument(_, v) -> v

  arg-name: x -> <concat-strings>["arg_", x]

  extract-keyword-args : Argument(v@Variable(_)) -> result
  with result := <concat-strings>[<to-python>v, "=None"]

  extract-keyword-args : DefaultedArgument(value, v) -> result
  with result := <concat-strings>[<to-python>v, "=", <to-python>value]

  to-stream-args : ExplicitInput(StreamList(vars)) -> result
  with result := <map(to-python)>vars

  to-value-args : NoArguments() -> []

  to-value-args : Arguments(ArgumentList(vars)) -> result
  with result := <map(extract-keyword-args)>vars

  to-output-args : ExplicitOutput(StreamList(vars)) -> result
  with e := <map(to-python)>vars
  with result := python|[return e;]|

  python-header = !python*|[
	import sys;
	import logging;
	from pipes_builtins import ident;
	from pipes_builtins import streamToString;
	from pipes_builtins import streamSize;
	from pipes_builtins import isEmpty;
	from pipes_builtins import main_const;
	run_pipes = [];
  ]|

  python-after = !python*|[
		def main(args): {
			for pipe in run_pipes: {
				result = pipe();
				print streamToString(result);
			};
		};
	    if __name__ == main_const: {
			main(sys.argv);
	    };
  ]|

  to-python :
    Module(declarations) -> python*|[
	  stm1*
      stm
      stm2*
      stm3*
    ]|
    with stm1* := <python-header>
    with stm2* := <python-after>
    with stm := <map(to-python)>declarations
    with stm3* := <collect-all(run-python, conc)>declarations

  to-python : Load(name) -> python|[from x import *;]|
  with x := name

  to-python : Variable(name) -> result
  with result := <arg-name>name

  to-python : Selector(Selection(selection)) -> result
  with joined := <separate-by(|"/"); concat-strings>selection
  with result := <concat-strings>["selector('", joined, "')"]

  to-python : All() -> "all"

  to-python : String(value) -> value

  to-python : Integer(value) -> value

  function-name : LambdaFunction(PipeName(name), _) -> result
  with result := <concat-strings>["lambda: func_", name]

  extract-pipe-streams : Pipe(_, ExplicitInput(StreamList(vars)), _, _, _) -> vars

  extract-function-streams : PipeName(name) -> result
  with p := <GetPipe>name
  with vars := <extract-pipe-streams>p
  with result := <map(to-python)>vars

  // extract-function-streams : ExtractFunction() -> []

  extract-function-arg : FunctionArgument(y, x) -> result
  with y' := <to-python>y
  with name := <arg-name>x
  with result := NamedParam(name,y')

  extract-function-args : None() -> []

  extract-function-args : Some(FunctionArguments(args)) -> result
  with result := <map(extract-function-arg)>args

  function-args: LambdaFunction(name, args) -> result
  with function_streams := <extract-function-streams>name
  with function_args := <extract-function-args>args
  with result := <concat>[function_streams, function_args]

  lambda-to-python: LambdaFunction(name, args) -> result
  with x_funcname := <function-name>
  with pe0* := <function-args>
  with result := py-exp|[
	ident(x_funcname(pe0*))
  ]|

  to-python : FunctionApplication(MultipleStreamAssignment(StreamList(list)), func, appl) -> python*|[
	x_streamname = e();
	logging.debug(streamSize(x_streamname));
  ]|
  with x_streamname := <map(to-python); separate-by(|","); concat-strings>list
  with e := <lambda-to-python>func

  to-python : IsEmptyCondition(Stream(name)) -> py-exp|[isEmpty(x)]|
  with x := <concat-strings>["stream_", name]

  to-python : Variable(x) -> result
  with result := <arg-name>x

  to-python : Stream(v) -> result
  with result := <concat-strings>["stream_", v]

  to-python : Then(t) -> python*|[
	stm*
  ]|
  with stm* := <map(to-python)>t

  to-python : Then([]) -> python*|[
	pass;
  ]|

  to-python : Else(t) -> python*|[
	stm*
  ]|
  with stm* := <map(to-python)>t

  to-python : Else([]) -> python*|[
	pass;
  ]|

  to-python : Conditional(If(t), then_block, else_block) -> [result]
  with e := <to-python>t
  with stm0* := <to-python>then_block
  with stm1* := <to-python>else_block
  with result := python|[
	if e: {
		stm0*
	} else: {
		stm1*
	};
  ]|

  to-python :
	Run(PipeName(name)) -> []

  run-python :
	Run(PipeName(name)) -> python|[run_pipes.append(e);]|
	with e := <concat-strings>["func_", name]

  to-python :
	Pipe(PipeName(name), input, arguments, statements, output) -> python*|[
		def x_name(e*): {
			stm*
			stm_ret
		};
		]|
		with x_name := <concat-strings> ["func_", name]
		with stream-args := <to-stream-args>input
		with value-args := <to-value-args>arguments
		with e* := <flatten-list>[stream-args, value-args]
		with stm_ret := <to-output-args>output
		with stm* := <map(to-python); concat>statements

  generate-python :
	Module(_) -> result
	with code := <to-python; flatten-list>
	with result := Application(code)

  pp-python :
  	ast -> s
  	where tbl := <import-term(lib/Python-pretty.pp.af)>
    where s := <ast2box(|[tbl]); box2text-string(|80)> ast
