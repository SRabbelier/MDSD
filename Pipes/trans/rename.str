module rename

imports
  libstratego-lib
  include/Pipes
  lib/editor-common.generated


rules
	rename-all= alltd(rename)
	
strategies
	rename: 
		Pipe(name, input, args, stat, output) -> Pipe(name, rn-input, rn-args, rn-stat, rn-output)
		with {| RenameStream
			: rn-input 	:= <rename>input
			; rn-args 	:= <rename-all>args
			; rn-stat 	:= <rename-all>stat
			; rn-output := <rename-all>output
		|}
	
	rename:
		Arguments(ArgumentList(list)) -> Arguments(ArgumentList(list'))
		with list' := <map(rename-variable)>list
	
	rename:
		ExplicitInput(StreamList(list)) -> ExplicitInput(StreamList(newlist))
		with newlist := <map(rename-stream <+ id)>list
	
	rename:
		FunctionApplication(MultipleStreamAssignment(StreamList(list)), lambda, type)
			-> FunctionApplication(MultipleStreamAssignment(StreamList(newlist)), lambda', type')
		with type':= <rename-all>type
		with lambda' := <rename-all>lambda
		with newlist := <map(rename-stream <+ id)>list
		
	rename:
		Stream(x) -> Stream(y)
		// with <debug(!"RN ")>x
		where y:= <RenameStream>x
		
	rename:
		Variable(x) -> Variable(y)
		where y := <RenameVariable>x
	
	rename-stream:
		init@Stream(x) -> Stream(y)
		where not(<RenameStream>x)
		with y:= x{<new>};
			rules(
				RenameStream: x -> y
				InitStream: y -> init
			)
	
	rename-variable:
		Argument(init@Variable(x)) -> Variable(y)
		with y:= x{<new>};
			rules(
				RenameVariable: x -> y
				InitVariable: y -> init
			)
	
	rename-variable:
		DefaultedArgument(def, init@Variable(x)) -> DefaultedArgument(def, Variable(y))
		with y:= x{<new>};
			rules(
				RenameVariable: x -> y
				InitVariable: y -> init
			)
		
