module rename

imports
  libstratego-lib
  include/Pipes
  lib/editor-common.generated


rules
	rename-all= alltd(rename)
	
strategies
	rename: 
		Pipe(name, input, args, stat, output) -> Pipe(name, rn-input, rn-args, rn-stat, rn-output)
		with {| RenameStream, RenameVariable
			: rn-input 	:= <rename-all>input
			; rn-args 	:= <rename-all>args
			; rn-stat 	:= <rename-all>stat
			; rn-output := <rename-all>output
		|}
	
	rename:
		Native(name, input, args, output) -> Native(name, input, rn-args, rn-output)
		with {| RenameStream, RenameVariable
			: rn-args	:= <rename-all>args
			; rn-output := <rename-all>output
		|}
	
	rename:
		Arguments(ArgumentList(list)) -> Arguments(ArgumentList(list'))
		with list' := <map(rename-variable)>list
	
	rename:
		ExplicitInput(StreamList(list)) -> ExplicitInput(StreamList(newlist))
		with newlist := <map(rename-stream <+ rename <+ id)>list
	
	rename:
		FunctionApplication(MultipleStreamAssignment(StreamList(list)), lambda, type)
			-> FunctionApplication(MultipleStreamAssignment(StreamList(newlist)), lambda', type')
		with type':= <rename-all>type
		with newlist := <map(rename-stream <+ rename <+ id)>list
		with lambda' := <rename-all>lambda
		
	rename:
		Stream(x) -> Stream(y)
		where y:= <RenameStream>x
		
	rename:
		Variable(x) -> Variable(y)
		where y := <RenameVariable>x
	
	rename-stream:
		init@Stream(x) -> Stream(y)
		where not(<RenameStream>x)
		with y:= x{<new>};
			rules(
				RenameStream: x -> y
				InitStream: y -> init
			)
	
	rename-variable:
		Argument(init@Variable(x)) -> Variable(y)
		with y:= x{<new>};
			rules(
				RenameVariable: x -> y
				InitVariable: y -> init
			)
	
	rename-variable:
		DefaultedArgument(def, init@Variable(x)) -> DefaultedArgument(def, Variable(y))
		with y:= x{<new>};
			rules(
				RenameVariable: x -> y
				InitVariable: y -> init
			)
		
